load("@rules_cc//cc:cc_binary.bzl", "cc_binary")
load("@rules_cc//cc:defs.bzl", "cc_library", "cc_test")
load("@rules_cuda//cuda:defs.bzl", "cuda_library", "requires_cuda")

package(default_visibility = ["//visibility:public"])

SUPPORTED_CPP_STANDARDS = [
    "17",
    "20",
    "23",
    "2b",
    "26",
]

[config_setting(
    name = "is_cpp" + v,
    values = {"cxxopt": "-std=c++" + v},
) for v in SUPPORTED_CPP_STANDARDS]

cc_library(
    name = "ucx_context_def_lib",
    hdrs = [
        "ucx_context_def.h",
    ],
)

cc_library(
    name = "ucx_connection",
    srcs = [
        "ucx_connection.cpp",
    ],
    hdrs = [
        "ucx_connection.hpp",
        "ucx_context_logger.hpp",
    ],
    target_compatible_with = select(
        {":is_cpp" + v: [] for v in SUPPORTED_CPP_STANDARDS} |
        {"//conditions:default": ["@platforms//:incompatible"]},
    ),
    deps = [
        ":ucx_context_def_lib",
        "@ucx//:ucx_runtime",
    ],
)

cc_library(
    name = "ucx_connection_manager",
    srcs = [
        "ucx_connection_manager.cpp",
    ],
    hdrs = [
        "lock_free_queue.hpp",
        "ucx_connection_manager.hpp",
    ],
    target_compatible_with = select(
        {":is_cpp" + v: [] for v in SUPPORTED_CPP_STANDARDS} |
        {"//conditions:default": ["@platforms//:incompatible"]},
    ),
    deps = [
        ":ucx_connection",
    ],
)

cc_library(
    name = "ucx_memory_resource_lib",
    srcs = [
        "ucx_memory_resource.cpp",
    ],
    hdrs = [
        "ucx_memory_resource.hpp",
    ],
    target_compatible_with = select(
        {":is_cpp" + v: [] for v in SUPPORTED_CPP_STANDARDS} |
        {"//conditions:default": ["@platforms//:incompatible"]},
    ),
    deps = [
        ":ucx_context_def_lib",
    ],
)

cc_library(
    name = "ucx_context_data_lib",
    hdrs = [
        "ucx_context_data.hpp",
    ],
    deps = [
        ":ucx_memory_resource_lib",
    ],
)

cc_library(
    name = "ucx_am_context",
    srcs = [
        "ucx_am_context/ucx_am_context.cpp",
    ],
    hdrs = [
        "ucx_am_context/ucx_am_context.hpp",
        "ucx_context_concept.hpp",
        "ucx_context_def.h",
        "ucx_context_logger.hpp",
        "ucx_device_context.hpp",
        "ucx_status.hpp",
    ],
    linkopts = ["-lrt"],
    target_compatible_with = select(
        {":is_cpp" + v: [] for v in SUPPORTED_CPP_STANDARDS} |
        {"//conditions:default": ["@platforms//:incompatible"]},
    ),
    deps = [
        ":ucx_connection",
        ":ucx_connection_manager",
        ":ucx_context_data_lib",
        "@ucx",
        "@unifex",
    ],
)

cc_library(
    name = "ucx_cuda_memory_manager",
    hdrs = [
        "cuda/ucx_cuda_macro.h",
        "cuda/ucx_cuda_memory_manager.hpp",
    ],
    deps = [
        "@local_cuda//:cuda",
    ],
)

cuda_library(
    name = "ucx_am_context_test_cuda_helper",
    srcs = ["ucx_am_context/ucx_am_context_test_cuda_helper.cu"],
    hdrs = [
        "ucx_am_context/ucx_am_context_test_helper.h",
        "ucx_context_def.h",
    ],
    target_compatible_with = requires_cuda(),
)

cc_library(
    name = "ucx_cuda_context",
    hdrs = [
        "cuda/ucx_cuda_context.hpp",
        "cuda/ucx_cuda_macro.h",
    ],
    deps = [
        "@local_cuda//:cuda",
    ],
)

cc_library(
    name = "ucx_am_context_test_host_helper",
    srcs = ["ucx_am_context/ucx_am_context_test_host_helper.cpp"],
    hdrs = [
        "ucx_am_context/ucx_am_context_test_helper.h",
        "ucx_context_def.h",
    ],
)

cc_test(
    name = "ucx_context_data_test",
    srcs = ["ucx_context_data_test.cpp"],
    target_compatible_with = select(
        {":is_cpp" + v: [] for v in SUPPORTED_CPP_STANDARDS} |
        {"//conditions:default": ["@platforms//:incompatible"]},
    ),
    deps = [
        ":ucx_context_data_lib",
        "@googletest//:gtest_main",
    ],
)

cc_test(
    name = "ucx_am_connection_test",
    srcs = ["ucx_am_context/ucx_am_connection_test.cpp"],
    target_compatible_with = select(
        {":is_cpp" + v: [] for v in SUPPORTED_CPP_STANDARDS} |
        {"//conditions:default": ["@platforms//:incompatible"]},
    ),
    deps = [
        ":ucx_connection",
        "@googletest//:gtest_main",
    ],
)

cc_test(
    name = "ucx_am_context_test",
    srcs = [
        "ucx_am_context/ucx_am_context_test.cpp",
    ],
    copts = [
        "-fcoroutines",
    ],
    defines = select({
        "@rules_cuda//cuda:is_enabled": ["CUDA_ENABLED=1"],
        "//conditions:default": ["CUDA_ENABLED=0"],
    }),
    target_compatible_with = select(
        {":is_cpp" + v: [] for v in SUPPORTED_CPP_STANDARDS} |
        {"//conditions:default": ["@platforms//:incompatible"]},
    ),
    deps = select({
        "@rules_cuda//cuda:is_enabled": [
            ":ucx_am_context",
            ":ucx_am_context_test_cuda_helper",
            ":ucx_am_context_test_host_helper",
            ":ucx_cuda_context",
            ":ucx_cuda_memory_manager",
            "@googletest//:gtest_main",
            "@local_cuda//:cuda",
            "@unifex",
        ],
        "//conditions:default": [
            ":ucx_am_context",
            ":ucx_am_context_test_host_helper",
            "@googletest//:gtest_main",
            "@unifex",
        ],
    }),
)

cc_binary(
    name = "ucx_am_context_perf",
    srcs = ["ucx_am_context/ucx_am_context_perf.cpp"],
    copts = [
        "-fcoroutines",
    ],
    linkstatic = False,  # Important for OpenUCX specific linking
    target_compatible_with = select(
        {":is_cpp" + v: [] for v in SUPPORTED_CPP_STANDARDS} |
        {"//conditions:default": ["@platforms//:incompatible"]},
    ),
    deps = [
        ":ucx_am_context",
        "@unifex",
    ],
)
